import groovy.io.FileType

// Configure the gradle enterprise plugin to enable build scans. Enabling the plugin at the top of the settings file allows the build scan to record
// as much information as possible.
plugins {
    id "com.gradle.enterprise" version "3.4.1"
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}

rootProject.name = 'airbyte'

def projects = []

def rootPath = rootDir.toPath()
rootPath.eachFileRecurse(FileType.FILES) { path ->
    def relativePath = rootPath.relativize(path)
    if (relativePath.endsWith('build.gradle') && relativePath.getParent()) {
        def projectName = ":${relativePath.parent.join(':')}"
        if (!projectName.contains(':build:') && !projectName.contains(':airbyte-integrations:')) {
            projects.add(":${relativePath.parent.join(':')}")
        }
    }
}
projects.sort(true)

include(*projects)
